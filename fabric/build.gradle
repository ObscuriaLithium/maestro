plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'dev.obscuria.hekate'
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    // Dependencies
    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_version}")
    modImplementation("dev.obscuria:fragmentum-fabric:${minecraft_version}-${fragmentum_version}")
    // Environment
    modLocalRuntime("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:21.11.0")
    modLocalRuntime("curse.maven:modmenu-308702:7441070")
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements', 'includeInternal', 'modCompileClasspath'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}

sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'fabric')
            }
        }
    }
}

loom.remapConfigurations.configureEach {
    configurations.named(it.name) {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}

modrinth {
    autoAddDependsOn = false
}

hekate {
    metadata = uri(metadata_lookup)
}
